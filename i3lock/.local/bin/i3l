#!/usr/bin/env zsh
# Take a screenshot first
maim /tmp/screen.png

# Figure out the dimensions of the screen
screenheight=$(identify -format "%H" /tmp/screen.png)
screenwidth=$(identify -format "%W" /tmp/screen.png)

# Make sure the comic fits inside it well
maxheight=${$((${screenheight}*.98))%%.*}

# How blurry should we make it?
# (Smaller pixscale=bigger pixels)
#pixscale=5
pixscale=$(( ( RANDOM % 20 ) + 1))
rescale=${$((1./${pixscale}*10000))%%.*}

# Pixelate!
convert /tmp/screen.png -scale ${pixscale}% -scale ${rescale}% /tmp/screen.png

# Now we get our comic and alt text
eval $(~/.local/bin/randxkcd.sh)

# and the width of that image, for the alt text
width=$(identify -format "%W" $filename)

# What font should imagemagick use?
#fontname=Humor-Sans
fontname=xkcd-Regular

# Set up the xkcd alt text and append to the comic image
captionoptions=(-background white -fill black -gravity center -font ${fontname} -pointsize 14 -size ${width})
convert "${captionoptions[@]}" caption:"Comic #${number}: ${title}" -append ${filename} "${captionoptions[@]}" caption:"${alttext}" -bordercolor white -border 5 -bordercolor black -border 1 -append -resize "x${maxheight}>" /tmp/xkcd-alt.png

# Stick the comic smack dab in the middle of the pixelated background
convert -gravity center -composite /tmp/screen.png /tmp/xkcd-alt.png /tmp/screen.png

# Finally, lock the screen!
i3lock -efi /tmp/screen.png

# Clean up after ourselves
for file in screen.png xkcd.png xkcd-alt.png; do
  [[ -f /tmp/$file ]] && rm /tmp/$file
done
